plugins {
    id 'java-library'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'com.emercurius'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Data Commons
    api 'org.springframework.data:spring-data-commons:3.5.2'

    // JPA/Hibernate (if needed for other non-Spring Data components)
    implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
    implementation 'org.hibernate:hibernate-core:6.4.4.Final'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
    annotationProcessor 'javax.annotation:javax.annotation-api:1.3.2'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation:3.1.5'

    api "io.grpc:grpc-stub:${grpcVersion}"
    api "io.grpc:grpc-protobuf:${grpcVersion}"
    // For Java 9+ TLS ALPN support at runtime
    runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protobufVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all().forEach { task ->
            task.plugins { grpc {} }
            task.builtins { java {} }
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs "${project.buildDir}/generated/source/proto/main/java",
                    "${project.buildDir}/generated/source/proto/main/grpc"
        }
        proto {
            srcDir 'src/main/proto'
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('processResources') {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
